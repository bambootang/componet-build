// Top-level build file where you can add configuration options common to all sub-projects/modules.

    buildscript {
        repositories {
            jcenter()
            google()
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:3.0.1'

            // NOTE: Do not place your application dependencies here; they belong
            // in the individual module build.gradle files
        }
    }


    allprojects {
        apply plugin: 'maven'

        repositories {
            jcenter()
            google()
            maven {
                url 'file://' + project.rootProject.rootDir + File.separator + '.repo-tmp'
            }
        }

        configurations.all {
            // check for updates every build
            resolutionStrategy.cacheChangingModulesFor 1, 'seconds'
        }
    }


    subprojects { subp ->
        //发布成的group名称
        project.group = 'com.bamboo.component'
        subp.afterEvaluate {

            if (!(subp.version + '').equals('unspecified')) {

                if (subp.extensions.findByName('android') != null
                        && !getPlugins().hasPlugin("com.android.application")) {
                    android.libraryVariants.all { variant ->
                        if (variant.name.equals('release')) {
                            def generateandroidSourcesJar = task("generate${variant.name.capitalize()}SourcesJar", type: Jar) {
                                classifier = 'sources'
                                from android.sourceSets.main.java.sourceFiles
                            }
                            artifacts {
                                archives generateandroidSourcesJar
                            }
                        }
                    }
                } else if (subp.extensions.findByName('android') == null) {
                    //java工程
                    //生成source 文件
                    task('sourcesJar', type: Jar) {
                        from sourceSets.main.java.srcDirs
                        classifier = 'sources'
                    }
                    artifacts {
                        archives sourcesJar
                    }
                }
                uploadArchives {
                    repositories.mavenDeployer {
                        repository(url: 'file://' + project.rootProject.rootDir + File.separator + '.repo-tmp')
                        pom.groupId = project.group
                        pom.artifactId = project.name
                        pom.version = project.version
                    }
                }
            }
        }
    }

